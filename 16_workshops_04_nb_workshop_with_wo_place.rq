#+ summary: How many workshops have and have *not* the information of their places? 
#+ endpoint: http://128.178.60.47:8890/sparql
#+ method: GET
#+ tags:
#+  - 16_workshop

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX core: <http://vocab.dhlab.epfl.ch/data-core#>
PREFIX common: <http://vocab.dhlab.epfl.ch/data-common#>
PREFIX grz-owl: <http://vocab.dhlab.epfl.ch/garzoni#>
PREFIX sem: <http://semanticweb.cs.vu.nl/2009/11/sem/> 

SELECT ?WorkshopWithPlaceInfo ?WorkshopWithoutPlaceInfo
WHERE 
{ 
  {SELECT (COUNT (distinct ?ws) AS ?WorkshopWithPlaceInfo) 
  WHERE {?ws a grz-owl:WorkshopMention ; grz-owl:hasLocation ?pl} 
  }
  {SELECT (COUNT (distinct ?ws) AS ?WorkshopWithoutPlaceInfo )
  WHERE {?ws a grz-owl:WorkshopMention . FILTER (NOT EXISTS{?ws grz-owl:hasLocation ?pl} ) }
  }
}
