
#+ summary: Average age difference between couples in 1427 in Tuscany
#+ endpoint: http://virtuoso.clariah-sdh.eculture.labs.vu.nl/sparql
#+ method: GET

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX catdim: <http://data.socialhistory.org/resource/catasto/dimension/>
PREFIX catocc: <http://data.socialhistory.org/resource/catasto/code/METIER/>
PREFIX catrel: <http://data.socialhistory.org/resource/catasto/code/relationToHead/>
PREFIX catasto: <http://data.socialhistory.org/resource/catasto/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>


SELECT (AVG(?agediff) as ?avg_agediff)
FROM <http://data.socialhistory.org/resource/catasto_donnees/assertion/bbe9f163/2017-07-02T17:42>
WHERE {
  ?husband catasto:relationToHead catrel:1 .
  ?husband catasto:sex <http://data.socialhistory.org/resource/catasto/code/sex/1> .
  ?husband catasto:inHousehold ?household .
  ?husband catasto:age ?age_husband .
  
  ?wife catasto:relationToHead catrel:1 .
  ?wife catasto:sex <http://data.socialhistory.org/resource/catasto/code/sex/2> .
  ?wife catasto:inHousehold ?household .
  ?wife catasto:age ?age_wife .
  
  BIND(?age_husband - ?age_wife AS ?agediff) .
} 
